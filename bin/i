#!/bin/env fish

function die; echo "error: $argv" >&2 && exit 1; end
function check; command -v $argv &>/dev/null || return 1 ; end
function confirm; read -P "$argv (y/N) " | string match -qr '^[Yy]$'; end
function menu; fzf -e --height 100 --preview-window=right:55% --preview "$argv"; end

if check apt
	if test -z "$argv"
		set sel (apt-cache pkgnames | menu "apt-cache show {}")
		test -n "$sel" && sudo apt install $sel
	else if test "$argv" = "update" -o "$argv" = "u"
		sudo apt update -y && sudo apt upgrade -y
	else
		sudo apt install $argv
	end

else if check pacman
	if test -z "$argv"
		set sel (pacman -Ssq | menu "pacman -Si {}")
		test -n "$sel" && sudo pacman -S --needed $sel
	else if test "$argv" = "update" -o "$argv" = "u"
		sudo pacman -Syu --noconfirm
	else
		sudo pacman -S --needed $argv && exit

		# AUR
		test (count $argv) -gt 1 && die "too many arguments"
		confirm "check the AUR for '$argv'" || exit
		mkdir -p "$HOME/.cache/aur"

		if test -d "$HOME/.cache/aur/$argv"
			cd "$HOME/.cache/aur/$argv" && git pull
		else
			git clone "https://aur.archlinux.org/$argv.git" \
				"$HOME/.cache/aur/$argv" || die "cloneing"
		end
		cd "$HOME/.cache/aur/$argv" && makepkg -si --noconfirm
	end

else
	die "unsupported distribution"
end
